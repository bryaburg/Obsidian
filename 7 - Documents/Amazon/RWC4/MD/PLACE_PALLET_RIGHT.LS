/PROG  PLACE_PALLET_RIGHT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "place pallet 4-6";
PROG_SIZE	= 4951;
CREATE		= DATE 19-08-12  TIME 14:41:48;
MODIFIED	= DATE 20-03-10  TIME 11:31:38;
FILE_NAME	= PLACE_PA;
VERSION		= 0;
LINE_COUNT	= 228;
MEMORY_SIZE	= 5495;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:   ;
   2:J PR[11:Pallet Place] 100% CNT75 Offset,PR[14:Above Pallet]    ;
   3:   ;
   4:  DO[45:OFF:Wait For Clear]=ON ;
   5:  WAIT DI[45:OFF:Clear To Place On Pall]=ON OR DI[46:OFF:Not Ok To Place on Pal]=ON    ;
   6:   ;
   7:  DO[45:OFF:Wait For Clear]=OFF ;
   8:   ;
   9:  !If the tote isn't fully seated ;
  10:  !in the grippers than jump to ;
  11:  !the end of the program and call ;
  12:  !place jackpot ;
  13:   ;
  14:  IF DI[46:OFF:Not Ok To Place on Pal]=ON,JMP LBL[98] ;
  15:   ;
  16:  !If placing the first tote on a ;
  17:  !pallet, measure the pallet ;
  18:  !height and adjust the user frame ;
  19:  !with the new height ;
  20:   ;
  21:  IF R[4:Tote Position]=1 AND R[3:Pallet Level]=1,CALL PALLET_HEIGHT_CHECK ;
  22:   ;
  23:  IF DO[63:OFF:Height Check Failed]=ON,JMP LBL[150] ;
  24:   ;
  25:  !If the pallet tote row is ;
  26:  !not 1, then jump to proper steps ;
  27:  !to do a vision inspection of ;
  28:  !the tote to place on top of ;
  29:   ;
  30:  IF R[3:Pallet Level]<>1,JMP LBL[9] ;
  31:   ;
  32:L PR[11:Pallet Place] 3000mm/sec CNT100 Tool_Offset,PR[15:Pick Z Offset]    ;
  33:  LBL[9] ;
  34:   ;
  35:  !If the pallet row to place is ;
  36:  !1, then jump to place and skip ;
  37:  !the vision inspection ;
  38:   ;
  39:  IF R[3:Pallet Level]=1,JMP LBL[10] ;
  40:   ;
  41:  DO[87:OFF:Lights ON]=ON ;
  42:   ;
  43:  PR[11,5:Pallet Place]=0    ;
  44:L PR[11:Pallet Place] 3000mm/sec FINE Tool_Offset,PR[30:Vision Pic Off]    ;
  45:  !Call the tote vision inspection ;
  46:   ;
  47:  CALL FIND_TOTE    ;
  48:  PR[46:vision shift oft]=PR[3:zero pos offset]    ;
  49:   ;
  50:   ;
  51:  !If The Vision Failed Three Times ;
  52:  !Move To An Offset Position And ;
  53:  !Try The Vision Again ;
  54:   ;
  55:  IF R[73: Vison Retry]<=2 AND R[72:Vison Pass/Fail]<=2,JMP LBL[30] ;
  56:   ;
  57:  !Move 5mm In Positive X Direction ;
  58:  !And Retry The Vision ;
  59:L PR[11:Pallet Place] 3000mm/sec FINE Tool_Offset,PR[30:Vision Pic Off] Offset,PR[41:vision pos x oft]    ;
  60:   ;
  61:  CALL FIND_TOTE    ;
  62:  PR[46:vision shift oft]=PR[41:vision pos x oft]    ;
  63:   ;
  64:   ;
  65:  IF R[73: Vison Retry]<=2 AND R[72:Vison Pass/Fail]<=2,JMP LBL[30] ;
  66:   ;
  67:  !Move 5mm In Negative X Direction ;
  68:  !And Retry The Vision ;
  69:   ;
  70:L PR[11:Pallet Place] 3000mm/sec FINE Tool_Offset,PR[30:Vision Pic Off] Offset,PR[42:vision neg x oft]    ;
  71:   ;
  72:  CALL FIND_TOTE    ;
  73:  PR[46:vision shift oft]=PR[42:vision neg x oft]    ;
  74:   ;
  75:   ;
  76:  IF R[73: Vison Retry]<=2 AND R[72:Vison Pass/Fail]<=2,JMP LBL[30] ;
  77:   ;
  78:  !Move 5mm In Positive Y Direction ;
  79:  !And Retry The Vision ;
  80:L PR[11:Pallet Place] 3000mm/sec FINE Tool_Offset,PR[30:Vision Pic Off] Offset,PR[43:vision pos y oft]    ;
  81:   ;
  82:  CALL FIND_TOTE    ;
  83:  PR[46:vision shift oft]=PR[43:vision pos y oft]    ;
  84:   ;
  85:   ;
  86:  IF R[73: Vison Retry]<=2 AND R[72:Vison Pass/Fail]<=2,JMP LBL[30] ;
  87:   ;
  88:  !Move 5mm In Negative Y Direction ;
  89:  !And Retry The Vision ;
  90:L PR[11:Pallet Place] 3000mm/sec FINE Tool_Offset,PR[30:Vision Pic Off] Offset,PR[44:vision neg y oft]    ;
  91:   ;
  92:  CALL FIND_TOTE    ;
  93:  PR[46:vision shift oft]=PR[44:vision neg y oft]    ;
  94:   ;
  95:   ;
  96:  LBL[30] ;
  97:   ;
  98:  !Zero out the vision offsets from ;
  99:  !the previous inspection and ;
 100:  !write the new offsets back into ;
 101:  !the offset registers ;
 102:   ;
 103:  R[75:Vision X Found]=0    ;
 104:  R[76:Vision Y Found]=0    ;
 105:  R[77:Vision R Found]=0    ;
 106:   ;
 107:  R[75:Vision X Found]=PR[31,1:Vision Offset]    ;
 108:  R[76:Vision Y Found]=PR[31,2:Vision Offset]    ;
 109:  R[77:Vision R Found]=PR[31,6:Vision Offset]    ;
 110:   ;
 111:  !If the vision failed its ;
 112:  !inspection 3 times or if the ;
 113:  !the offsets are to large, then ;
 114:  !jump to the bottom of the ;
 115:  !program and not place the tote ;
 116:   ;
 117:  IF R[72:Vison Pass/Fail]=3,JMP LBL[99] ;
 118:  IF R[75:Vision X Found]>60 OR R[75:Vision X Found]<(-60),JMP LBL[99] ;
 119:  IF R[76:Vision Y Found]>50 OR R[76:Vision Y Found]<(-50),JMP LBL[99] ;
 120:  IF R[77:Vision R Found]>20 OR R[77:Vision R Found]<(-20),JMP LBL[99] ;
 121:   ;
 122:  !If the tote place position is ;
 123:  !less than 4, jump over setting ;
 124:  !the y angle for placing ;
 125:  !Only need to place totes ;
 126:  !position 4 and 5 on an angle ;
 127:   ;
 128:  IF R[4:Tote Position]<4,JMP LBL[4] ;
 129:   ;
 130:  PR[11,5:Pallet Place]=R[63:Tote 3&4&5 P Ang]    ;
 131:   ;
 132:  LBL[4] ;
 133:   ;
 134:  !Set the position register with ;
 135:  !the vision offsets ;
 136:  !Multiple by negative one to ;
 137:  !to get proper direction ;
 138:   ;
 139:  PR[25,1:Vision Place Off]=R[75:Vision X Found]*(-1)    ;
 140:  PR[25,2:Vision Place Off]=R[76:Vision Y Found]*(-1)    ;
 141:  PR[25,6:Vision Place Off]=R[77:Vision R Found]*(-1)    ;
 142:   ;
 143:  PR[25,3:Vision Place Off]=10    ;
 144:  PR[32:Vision No Y Offt]=PR[30:Vision Pic Offt]    ;
 145:  PR[32,2:Vision No Y Offt]=0    ;
 146:   ;
 147:   ;
 148:  COL GUARD ADJUST 150 ;
 149:   ;
 150:L PR[11:Pallet Place] 3000mm/sec CNT75 Tool_Offset,PR[32:Vision No Y Of] Offset,PR[46:vision shift oft]    ;
 151:   ;
 152:L PR[11:Pallet Place] 3000mm/sec CNT50 Tool_Offset,PR[25:Vision Place O] Offset,PR[46:vision shift oft]    ;
 153:   ;
 154:  PR[46,3:vision shift oft]=PR[18,3:Place -Z Offset]    ;
 155:   ;
 156:L PR[11:Pallet Place] 3000mm/sec CNT25 Tool_Offset,PR[25:Vision Place O] Offset,PR[46:vision shift oft] AP_LD15 DB   50.0mm,CALL GRIPPER_OPEN    ;
 157:   ;
 158:  !Jump to label 10 is called above ;
 159:  !this is for placing on tote ;
 160:  !row one ;
 161:   ;
 162:  COL GUARD ADJUST 100 ;
 163:   ;
 164:  JMP LBL[11] ;
 165:  LBL[10] ;
 166:   ;
 167:  !Depending on what pallet you ;
 168:  !are placing on, set the y angle ;
 169:  !to positive or negative ;
 170:   ;
 171:  IF R[4:Tote Position]<4,JMP LBL[20] ;
 172:   ;
 173:  PR[11,5:Pallet Place]=R[63:Tote 3&4&5 P Ang]    ;
 174:   ;
 175:  LBL[20] ;
 176:   ;
 177:L PR[11:Pallet Place] 3000mm/sec FINE AP_LD15 DB   50.0mm,CALL GRIPPER_OPEN    ;
 178:   ;
 179:  LBL[11] ;
 180:   ;
 181:  DO[51:OFF:Placed Pallet]=ON ;
 182:   ;
 183:L PR[11:Pallet Place] 3000mm/sec CNT100 Tool_Offset,PR[15:Pick Z Offset]    ;
 184:  WAIT DI[51:OFF:Place Pallet Ack]=ON    ;
 185:  DO[51:OFF:Placed Pallet]=OFF ;
 186:  DO[46:OFF:Robot Tote Present]=OFF ;
 187:   ;
 188:  PAYLOAD[2:EOAT no tote] ;
 189:  CALL CLEARDATA    ;
 190:  RUN CLAMP_OPEN ;
 191:L PR[11:Pallet Place] 3000mm/sec CNT100 Offset,PR[14:Above Pallet]    ;
 192:   ;
 193:  !Depending on where you placed ;
 194:  !call the midpoint program to ;
 195:  !find a safe route for the next ;
 196:  !pick ;
 197:   ;
 198:  JMP LBL[100] ;
 199:   ;
 200:  !Jump label 99 comes from above ;
 201:  !If the vision failed or the ;
 202:  !offsets where to large or the ;
 203:  !tote present sensors didn't ;
 204:  !see a tote fully seated, then ;
 205:  !don't try to place that tote ;
 206:  !onto a pallet, currently placing ;
 207:  !it onto jackpot ;
 208:   ;
 209:  LBL[99] ;
 210:  DO[70:OFF:Vision Failed]=ON ;
 211:  LBL[98] ;
 212:  CALL CLAMP_CLOSE    ;
 213:  WAIT DI[82:OFF:End Clamps AT Work]=ON    ;
 214:L PR[11:Pallet Place] 3000mm/sec CNT100 Offset,PR[14:Above Pallet]    ;
 215:   ;
 216:  CALL HOME    ;
 217:   ;
 218:  LBL[100] ;
 219:  JMP LBL[200] ;
 220:   ;
 221:  !height check fail go home ;
 222:  LBL[150] ;
 223:L PR[11:Pallet Place] 3000mm/sec CNT100 Offset,PR[14:Above Pallet]    ;
 224:  CALL HOME    ;
 225:  DO[63:OFF:Height Check Failed]=OFF ;
 226:   ;
 227:  LBL[200] ;
 228:  DO[4:OFF:Called Place Pallet]=OFF ;
/POS
/END
