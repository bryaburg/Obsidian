/PROG  OFFSETS_PLASTIC_LEFT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "Pallets 1-3";
PROG_SIZE	= 2663;
CREATE		= DATE 20-02-17  TIME 12:41:20;
MODIFIED	= DATE 20-03-11  TIME 11:05:32;
FILE_NAME	= OFFSETS_;
VERSION		= 0;
LINE_COUNT	= 151;
MEMORY_SIZE	= 3075;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !This program is used to ;
   2:  !populate the place position ;
   3:  !register based off of what ;
   4:  !what pallet, row and position ;
   5:  !to place the tote ;
   6:   ;
   7:  !Zero out the vision offset and ;
   8:  !place postion register before ;
   9:  !populating the new position ;
  10:   ;
  11:  R[72:Vison Pass/Fail]=0    ;
  12:  R[75:Vision X Found]=0    ;
  13:  R[76:Vision Y Found]=0    ;
  14:  R[77:Vision R Found]=0    ;
  15:   ;
  16:  PR[25:Vision Place Off]=PR[26:Blank Offset]    ;
  17:  PR[11:Pallet Place]=PR[26:Blank Offset]    ;
  18:   ;
  19:  !Set the user frame of the robot ;
  20:  !depending on what pallet you ;
  21:  !are placing on ;
  22:   ;
  23:  UFRAME_NUM=R[1:Pallet Position] ;
  24:   ;
  25:  !Set position register values ;
  26:  !For tote X&Y&R place position ;
  27:   ;
  28:  !Tote #1 ;
  29:   ;
  30:  IF R[4:Tote Position]<>1,JMP LBL[12] ;
  31:   ;
  32:  PR[11,1:Pallet Place]=R[120:Tote 1 X Offset]    ;
  33:   ;
  34:  PR[11,2:Pallet Place]=R[121:Tote 1 Y Offset]*(-1)    ;
  35:   ;
  36:  PR[11,6:Pallet Place]=R[134:Tote 1&2&3 R Ang]    ;
  37:   ;
  38:  JMP LBL[17] ;
  39:  LBL[12] ;
  40:   ;
  41:  !Tote #2 ;
  42:   ;
  43:  IF R[4:Tote Position]<>2,JMP LBL[13] ;
  44:   ;
  45:  PR[11,1:Pallet Place]=R[122:Tote 2 X Offset]    ;
  46:   ;
  47:  PR[11,2:Pallet Place]=R[123:Tote 2 Y Offset]*(-1)    ;
  48:   ;
  49:  PR[11,6:Pallet Place]=R[134:Tote 1&2&3 R Ang]    ;
  50:   ;
  51:  JMP LBL[17] ;
  52:  LBL[13] ;
  53:   ;
  54:  !Tote #3 ;
  55:   ;
  56:  IF R[4:Tote Position]<>3,JMP LBL[14] ;
  57:   ;
  58:  PR[11,1:Pallet Place]=R[124:Tote 3 X Offset]    ;
  59:   ;
  60:  PR[11,2:Pallet Place]=R[125:Tote 3 Y Offset]*(-1)    ;
  61:   ;
  62:  PR[11,6:Pallet Place]=R[134:Tote 1&2&3 R Ang]    ;
  63:   ;
  64:  JMP LBL[17] ;
  65:  LBL[14] ;
  66:   ;
  67:  !Tote #4 ;
  68:   ;
  69:  IF R[4:Tote Position]<>4,JMP LBL[15] ;
  70:   ;
  71:  PR[11,1:Pallet Place]=R[126:Tote 4 X Offset]    ;
  72:   ;
  73:  PR[11,2:Pallet Place]=R[127:Tote 4 Y Offset]*(-1)    ;
  74:   ;
  75:  PR[11,6:Pallet Place]=R[137:Tote 4&5&6 R Ang]    ;
  76:   ;
  77:  JMP LBL[17] ;
  78:  LBL[15] ;
  79:   ;
  80:  !Tote #5 ;
  81:   ;
  82:  IF R[4:Tote Position]<>5,JMP LBL[16] ;
  83:   ;
  84:  PR[11,1:Pallet Place]=R[128:Tote 5 X Offset]    ;
  85:   ;
  86:  PR[11,2:Pallet Place]=R[129:Tote 5 Y Offset]*(-1)    ;
  87:   ;
  88:  PR[11,6:Pallet Place]=R[137:Tote 4&5&6 R Ang]    ;
  89:   ;
  90:  JMP LBL[17] ;
  91:  LBL[16] ;
  92:   ;
  93:  !Tote #6 ;
  94:   ;
  95:  IF R[4:Tote Position]<>6,JMP LBL[17] ;
  96:   ;
  97:  PR[11,1:Pallet Place]=R[130:Tote 6 X Offset]    ;
  98:   ;
  99:  PR[11,2:Pallet Place]=R[131:Tote 6 Y Offset]*(-1)    ;
 100:   ;
 101:  PR[11,6:Pallet Place]=R[137:Tote 4&5&6 R Ang]    ;
 102:   ;
 103:  LBL[17] ;
 104:   ;
 105:  !Set the Z height for the ;
 106:  !Row to place the tote ;
 107:   ;
 108:  IF R[3:Pallet Level]<>1,JMP LBL[20] ;
 109:   ;
 110:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]    ;
 111:  PR[14,3:Above Pallet]=1300    ;
 112:   ;
 113:  JMP LBL[25] ;
 114:  LBL[20] ;
 115:   ;
 116:  IF R[3:Pallet Level]<>2,JMP LBL[21] ;
 117:   ;
 118:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]    ;
 119:  PR[14,3:Above Pallet]=1050    ;
 120:   ;
 121:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 122:   ;
 123:  JMP LBL[25] ;
 124:  LBL[21] ;
 125:   ;
 126:  IF R[3:Pallet Level]<>3,JMP LBL[22] ;
 127:   ;
 128:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]    ;
 129:  PR[14,3:Above Pallet]=800    ;
 130:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 131:   ;
 132:  JMP LBL[25] ;
 133:  LBL[22] ;
 134:   ;
 135:  IF R[3:Pallet Level]<>4,JMP LBL[23] ;
 136:   ;
 137:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]    ;
 138:  PR[14,3:Above Pallet]=550    ;
 139:   ;
 140:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 141:   ;
 142:  JMP LBL[25] ;
 143:  LBL[23] ;
 144:   ;
 145:  IF R[3:Pallet Level]<>5,JMP LBL[25] ;
 146:   ;
 147:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]+R[67:Tote Row 5 Z Ht]    ;
 148:  PR[14,3:Above Pallet]=300    ;
 149:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 150:   ;
 151:  LBL[25] ;
/POS
/END
