/PROG  OFFSETS_LEFT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3109;
CREATE		= DATE 18-04-23  TIME 06:23:20;
MODIFIED	= DATE 20-03-10  TIME 15:37:32;
FILE_NAME	= OFFSETS;
VERSION		= 0;
LINE_COUNT	= 186;
MEMORY_SIZE	= 3641;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !This program is used to ;
   2:  !populate the place position ;
   3:  !register based off of what ;
   4:  !what pallet, row and position ;
   5:  !to place the tote ;
   6:   ;
   7:  !Zero out the vision offset and ;
   8:  !place postion register before ;
   9:  !populating the new position ;
  10:   ;
  11:  R[72:Vison Pass/Fail]=0    ;
  12:  R[75:Vision X Found]=0    ;
  13:  R[76:Vision Y Found]=0    ;
  14:  R[77:Vision R Found]=0    ;
  15:   ;
  16:  PR[25:Vision Place Off]=PR[26:Blank Offset]    ;
  17:  PR[11:Pallet Place]=PR[26:Blank Offset]    ;
  18:   ;
  19:  !Set the user frame of the robot ;
  20:  !depending on what pallet you ;
  21:  !are placing on ;
  22:   ;
  23:  UFRAME_NUM=R[1:Pallet Position] ;
  24:   ;
  25:  !Set position register values ;
  26:  !For tote X&Y&R place position ;
  27:   ;
  28:  !Tote #1 ;
  29:   ;
  30:  IF R[4:Tote Position]<>1,JMP LBL[12] ;
  31:   ;
  32:  PR[11,1:Pallet Place]=R[50:Tote 1 X Offset]    ;
  33:   ;
  34:  IF R[1:Pallet Position]<5,JMP LBL[30] ;
  35:   ;
  36:  PR[11,2:Pallet Place]=R[51:Tote 1 Y Offset]    ;
  37:   ;
  38:  JMP LBL[31] ;
  39:  LBL[30] ;
  40:   ;
  41:  PR[11,2:Pallet Place]=R[51:Tote 1 Y Offset]*(-1)    ;
  42:   ;
  43:  LBL[31] ;
  44:   ;
  45:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  46:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]    ;
  47:   ;
  48:  JMP LBL[17] ;
  49:  LBL[12] ;
  50:   ;
  51:  !Tote #2 ;
  52:   ;
  53:  IF R[4:Tote Position]<>2,JMP LBL[13] ;
  54:   ;
  55:  PR[11,1:Pallet Place]=R[52:Tote 2 X Offset]    ;
  56:   ;
  57:  IF R[1:Pallet Position]<5,JMP LBL[32] ;
  58:   ;
  59:  PR[11,2:Pallet Place]=R[53:Tote 2 Y Offset]    ;
  60:   ;
  61:  JMP LBL[33] ;
  62:  LBL[32] ;
  63:   ;
  64:  PR[11,2:Pallet Place]=R[53:Tote 2 Y Offset]*(-1)    ;
  65:   ;
  66:  LBL[33] ;
  67:   ;
  68:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  69:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]+180    ;
  70:   ;
  71:  JMP LBL[17] ;
  72:  LBL[13] ;
  73:   ;
  74:  !Tote #3 ;
  75:   ;
  76:  IF R[4:Tote Position]<>3,JMP LBL[14] ;
  77:   ;
  78:  PR[11,1:Pallet Place]=R[54:Tote 3 X Offset]    ;
  79:   ;
  80:  IF R[1:Pallet Position]<5,JMP LBL[34] ;
  81:   ;
  82:  PR[11,2:Pallet Place]=R[55:Tote 3 Y Offset]    ;
  83:   ;
  84:  JMP LBL[35] ;
  85:  LBL[34] ;
  86:   ;
  87:  PR[11,2:Pallet Place]=R[55:Tote 3 Y Offset]*(-1)    ;
  88:   ;
  89:  LBL[35] ;
  90:   ;
  91:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  92:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
  93:   ;
  94:  JMP LBL[17] ;
  95:  LBL[14] ;
  96:   ;
  97:  !Tote #4 ;
  98:   ;
  99:  IF R[4:Tote Position]<>4,JMP LBL[15] ;
 100:   ;
 101:  PR[11,1:Pallet Place]=R[56:Tote 4 X Offset]    ;
 102:   ;
 103:  IF R[1:Pallet Position]<4,JMP LBL[36] ;
 104:   ;
 105:  PR[11,2:Pallet Place]=R[57:Tote 4 Y Offset]    ;
 106:   ;
 107:  JMP LBL[37] ;
 108:  LBL[36] ;
 109:   ;
 110:  PR[11,2:Pallet Place]=R[57:Tote 4 Y Offset]*(-1)    ;
 111:   ;
 112:  LBL[37] ;
 113:   ;
 114:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 115:   ;
 116:  JMP LBL[17] ;
 117:  LBL[15] ;
 118:   ;
 119:  !Tote #5 ;
 120:   ;
 121:  IF R[4:Tote Position]<>5,JMP LBL[17] ;
 122:   ;
 123:  PR[11,1:Pallet Place]=R[58:Tote 5 X Offset]    ;
 124:   ;
 125:  IF R[1:Pallet Position]<5,JMP LBL[38] ;
 126:   ;
 127:  PR[11,2:Pallet Place]=R[59:Tote 5 Y Offset]    ;
 128:   ;
 129:  JMP LBL[39] ;
 130:  LBL[38] ;
 131:   ;
 132:  PR[11,2:Pallet Place]=R[59:Tote 5 Y Offset]*(-1)    ;
 133:   ;
 134:  LBL[39] ;
 135:   ;
 136:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 137:   ;
 138:  LBL[17] ;
 139:   ;
 140:  !Set the Z height for the ;
 141:  !Row to place the tote ;
 142:   ;
 143:  IF R[3:Pallet Level]<>1,JMP LBL[20] ;
 144:   ;
 145:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]    ;
 146:  PR[14,3:Above Pallet]=1300    ;
 147:   ;
 148:  JMP LBL[25] ;
 149:  LBL[20] ;
 150:   ;
 151:  IF R[3:Pallet Level]<>2,JMP LBL[21] ;
 152:   ;
 153:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]    ;
 154:  PR[14,3:Above Pallet]=1050    ;
 155:   ;
 156:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 157:   ;
 158:  JMP LBL[25] ;
 159:  LBL[21] ;
 160:   ;
 161:  IF R[3:Pallet Level]<>3,JMP LBL[22] ;
 162:   ;
 163:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]    ;
 164:  PR[14,3:Above Pallet]=800    ;
 165:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 166:   ;
 167:  JMP LBL[25] ;
 168:  LBL[22] ;
 169:   ;
 170:  IF R[3:Pallet Level]<>4,JMP LBL[23] ;
 171:   ;
 172:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]    ;
 173:  PR[14,3:Above Pallet]=550    ;
 174:   ;
 175:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 176:   ;
 177:  JMP LBL[25] ;
 178:  LBL[23] ;
 179:   ;
 180:  IF R[3:Pallet Level]<>5,JMP LBL[25] ;
 181:   ;
 182:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]+R[67:Tote Row 5 Z Ht]    ;
 183:  PR[14,3:Above Pallet]=300    ;
 184:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 185:   ;
 186:  LBL[25] ;
/POS
/END
