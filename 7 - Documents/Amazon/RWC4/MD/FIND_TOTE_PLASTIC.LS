/PROG  FIND_TOTE_PLASTIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2477;
CREATE		= DATE 20-02-17  TIME 12:46:58;
MODIFIED	= DATE 20-02-17  TIME 12:46:58;
FILE_NAME	= FIND_TOT;
VERSION		= 0;
LINE_COUNT	= 121;
MEMORY_SIZE	= 2881;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !This is used to find the ;
   2:  !to offset the place point ;
   3:   ;
   4:  DO[70:OFF:Vision Failed]=OFF ;
   5:  R[72:Vison Pass/Fail]=0    ;
   6:  R[73: Vison Retry]=0    ;
   7:   ;
   8:  DO[87:OFF:Lights ON]=ON ;
   9:   ;
  10:  LBL[5] ;
  11:  LBL[6] ;
  12:  UFRAME_NUM=R[1:Pallet Position] ;
  13:  UTOOL_NUM=3 ;
  14:  PR[31,1:Vision Offset]=0    ;
  15:  PR[31,2:Vision Offset]=0    ;
  16:  PR[31,3:Vision Offset]=0    ;
  17:  PR[31,4:Vision Offset]=0    ;
  18:  PR[31,5:Vision Offset]=0    ;
  19:  PR[31,6:Vision Offset]=0    ;
  20:   ;
  21:  !If you are on rows 3 and 5 ;
  22:  !use side b inspection ;
  23:   ;
  24:  IF R[3:Pallet Level]=2 OR R[3:Pallet Level]=4,JMP LBL[50] ;
  25:   ;
  26:  !side a vision inspections ;
  27:   ;
  28:  VISION OVERRIDE 'SIDEAEXPOSURETIME' 10.0 ;
  29:   ;
  30:  VISION RUN_FIND 'SIDEA_CORNORLOCATION'    ;
  31:  VISION GET_OFFSET 'SIDEA_CORNORLOCATION' VR[1] JMP LBL[16] ;
  32:   ;
  33:  JMP LBL[90] ;
  34:   ;
  35:  LBL[16] ;
  36:  R[72:Vison Pass/Fail]=R[72:Vison Pass/Fail]+1    ;
  37:   ;
  38:  VISION OVERRIDE 'SIDEAEXPOSURETIME' 15.0 ;
  39:   ;
  40:  VISION RUN_FIND 'SIDEA_CORNORLOCATION'    ;
  41:  VISION GET_OFFSET 'SIDEA_CORNORLOCATION' VR[1] JMP LBL[18] ;
  42:   ;
  43:  JMP LBL[90] ;
  44:   ;
  45:  LBL[18] ;
  46:  R[72:Vison Pass/Fail]=R[72:Vison Pass/Fail]+1    ;
  47:   ;
  48:  VISION OVERRIDE 'SIDEAEXPOSURETIME' 20.0 ;
  49:   ;
  50:  VISION RUN_FIND 'SIDEA_CORNORLOCATION'    ;
  51:  VISION GET_OFFSET 'SIDEA_CORNORLOCATION' VR[1] JMP LBL[80] ;
  52:   ;
  53:  JMP LBL[90] ;
  54:   ;
  55:  !side B vision inspection ;
  56:  LBL[50:side B inspectin] ;
  57:   ;
  58:  VISION OVERRIDE 'SIDEBEXPOSURETIME' 10.0 ;
  59:   ;
  60:  VISION RUN_FIND 'SIDEB_CORNORLOCATION'    ;
  61:  VISION GET_OFFSET 'SIDEB_CORNORLOCATION' VR[1] JMP LBL[52] ;
  62:   ;
  63:  JMP LBL[90] ;
  64:   ;
  65:  LBL[52] ;
  66:   ;
  67:  R[72:Vison Pass/Fail]=R[72:Vison Pass/Fail]+1    ;
  68:   ;
  69:  VISION OVERRIDE 'SIDEBEXPOSURETIME' 15.0 ;
  70:   ;
  71:  VISION RUN_FIND 'SIDEB_CORNORLOCATION'    ;
  72:  VISION GET_OFFSET 'SIDEB_CORNORLOCATION' VR[1] JMP LBL[54] ;
  73:   ;
  74:  JMP LBL[90] ;
  75:   ;
  76:  LBL[54] ;
  77:   ;
  78:  R[72:Vison Pass/Fail]=R[72:Vison Pass/Fail]+1    ;
  79:   ;
  80:  VISION OVERRIDE 'SIDEBEXPOSURETIME' 20.0 ;
  81:   ;
  82:  VISION RUN_FIND 'SIDEB_CORNORLOCATION'    ;
  83:  VISION GET_OFFSET 'SIDEB_CORNORLOCATION' VR[1] JMP LBL[80] ;
  84:   ;
  85:  JMP LBL[90] ;
  86:   ;
  87:  !all inspections failed ;
  88:  LBL[80:vision failed] ;
  89:  R[72:Vison Pass/Fail]=R[72:Vison Pass/Fail]+1    ;
  90:   ;
  91:  WAIT    .01(sec) ;
  92:  IF R[72:Vison Pass/Fail]<=2,JMP LBL[5] ;
  93:   ;
  94:   ;
  95:  LBL[90:vision passed] ;
  96:   ;
  97:  PR[31]=VR[1].OFFSET ;
  98:   ;
  99:  R[75:Vision X Found]=PR[31,1:Vision Offset]    ;
 100:  R[76:Vision Y Found]=PR[31,2:Vision Offset]    ;
 101:  R[77:Vision R Found]=PR[31,6:Vision Offset]    ;
 102:   ;
 103:  !If the vision failed its ;
 104:  !inspection 3 times or if the ;
 105:  !the offsets are to large, then ;
 106:  !jump to the top of the ;
 107:  !program and try the other ;
 108:  !inspection ;
 109:   ;
 110:  R[73: Vison Retry]=R[73: Vison Retry]+1    ;
 111:   ;
 112:  IF R[73: Vison Retry]=3,JMP LBL[99] ;
 113:  IF R[72:Vison Pass/Fail]=3,JMP LBL[99] ;
 114:   ;
 115:  IF R[75:Vision X Found]>60 OR R[75:Vision X Found]<(-60),JMP LBL[80] ;
 116:  IF R[76:Vision Y Found]>50 OR R[76:Vision Y Found]<(-50),JMP LBL[80] ;
 117:  IF R[77:Vision R Found]>20 OR R[77:Vision R Found]<(-20),JMP LBL[80] ;
 118:   ;
 119:   ;
 120:  LBL[99] ;
 121:  DO[87:OFF:Lights ON]=OFF ;
/POS
/END
