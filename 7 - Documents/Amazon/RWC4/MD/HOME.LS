/PROG  HOME
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2149;
CREATE		= DATE 19-01-07  TIME 17:07:46;
MODIFIED	= DATE 20-04-15  TIME 09:15:16;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 127;
MEMORY_SIZE	= 2553;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  DO[1:ON :Called Home]=ON ;
   2:   ;
   3:  UFRAME_NUM=0 ;
   4:  UTOOL_NUM=3 ;
   5:   ;
   6:  IF DI[25:OFF:Tote In Gripper]=OFF,JMP LBL[40] ;
   7:   ;
   8:  CALL CLAMP_CLOSE    ;
   9:   ;
  10:  LBL[40] ;
  11:  IF DO[40:ON :At Home]=ON,JMP LBL[99] ;
  12:   ;
  13:  PR[20:Homing Joint Pos]=JPOS    ;
  14:  PR[21:Homing World Pos]=LPOS    ;
  15:   ;
  16:  R[20:Homing J1 Pos]=PR[20,1:Homing Joint Pos]    ;
  17:  R[21:Homing J2 Pos]=PR[20,2:Homing Joint Pos]    ;
  18:  R[22:Homing J3 Pos]=PR[20,3:Homing Joint Pos]    ;
  19:  R[23:Homing J4 Pos]=PR[20,4:Homing Joint Pos]    ;
  20:  R[24:Homing J5 Pos]=PR[20,5:Homing Joint Pos]    ;
  21:  R[25:Homing J6 Pos]=PR[20,6:Homing Joint Pos]    ;
  22:   ;
  23:  R[26:Homing X Pos]=PR[21,1:Homing World Pos]    ;
  24:  R[27:Homing Y Pos]=PR[21,2:Homing World Pos]    ;
  25:  R[28:Homing Z Pos]=PR[21,3:Homing World Pos]    ;
  26:  R[29:Homing W Pos]=PR[21,4:Homing World Pos]    ;
  27:  R[30:Homing P Pos]=PR[21,5:Homing World Pos]    ;
  28:  R[31:Homing R Pos]=PR[21,6:Homing World Pos]    ;
  29:   ;
  30:  !is z high enough ;
  31:  IF R[28:Homing Z Pos]>200,JMP LBL[10] ;
  32:   ;
  33:  PR[5:Homing PosReg]=LPOS    ;
  34:  PR[5,3:Homing PosReg]=200    ;
  35:   ;
  36:L PR[5:Homing PosReg] 2000mm/sec FINE    ;
  37:   ;
  38:  LBL[10] ;
  39:   ;
  40:  !if on right side ;
  41:  IF R[20:Homing J1 Pos]>25,JMP LBL[20] ;
  42:   ;
  43:  !if on left side ;
  44:  IF R[20:Homing J1 Pos]<(-20),JMP LBL[30] ;
  45:   ;
  46:  JMP LBL[99] ;
  47:   ;
  48:  !homing from right ;
  49:  LBL[20] ;
  50:  !pallet pos 8 ;
  51:  IF R[20:Homing J1 Pos]>25 AND R[20:Homing J1 Pos]<50,JMP LBL[21] ;
  52:   ;
  53:  !pallet pos 7  ;
  54:  IF R[20:Homing J1 Pos]>50 AND R[20:Homing J1 Pos]<90,JMP LBL[22] ;
  55:   ;
  56:  !pallet pos 6&5 ;
  57:  IF R[20:Homing J1 Pos]>90,JMP LBL[23] ;
  58:   ;
  59:  JMP LBL[99] ;
  60:   ;
  61:  !homing from left ;
  62:  LBL[30] ;
  63:  !pallet pos 4 ;
  64:  IF R[20:Homing J1 Pos]<(-20) AND R[20:Homing J1 Pos]>(-50),JMP LBL[31] ;
  65:   ;
  66:  !pallet pos 3 ;
  67:  IF R[20:Homing J1 Pos]<(-50) AND R[20:Homing J1 Pos]>(-90),JMP LBL[32] ;
  68:   ;
  69:  !pallet pos 1&2 ;
  70:  IF R[20:Homing J1 Pos]<(-90),JMP LBL[33] ;
  71:   ;
  72:  JMP LBL[99] ;
  73:   ;
  74:  !from pallet 8 ;
  75:  LBL[21] ;
  76:  IF R[26:Homing X Pos]<1500,JMP LBL[99] ;
  77:   ;
  78:  PR[37,6:pallet6safepoint]=PR[6,6:currentlocation]    ;
  79:J PR[37:pallet6safepoint] 100% CNT100    ;
  80:   ;
  81:  JMP LBL[99] ;
  82:   ;
  83:  !from pallet 7 ;
  84:  LBL[22] ;
  85:   ;
  86:  JMP LBL[99] ;
  87:   ;
  88:  !from pallet pos 6&5 ;
  89:  LBL[23] ;
  90:J PR[35:Pallet 4&5SafePt] 100% CNT100    ;
  91:   ;
  92:  JMP LBL[99] ;
  93:   ;
  94:  !from pallet pos 4 ;
  95:  LBL[31] ;
  96:  IF R[26:Homing X Pos]<1500,JMP LBL[99] ;
  97:   ;
  98:   ;
  99:  PR[38,6:pallet3safepoint]=PR[6,6:currentlocation]    ;
 100:L PR[38:pallet3safepoint] max_speed CNT100    ;
 101:   ;
 102:  JMP LBL[99] ;
 103:   ;
 104:  !from pallet 3 ;
 105:  LBL[32] ;
 106:   ;
 107:  JMP LBL[99] ;
 108:   ;
 109:  !from pallet 1&2 ;
 110:  LBL[33] ;
 111:J PR[36:Pallet 1&2SafePt] 100% CNT100    ;
 112:   ;
 113:  JMP LBL[99] ;
 114:   ;
 115:  LBL[99] ;
 116:   ;
 117:J PR[1:Home Pos] 100% FINE    ;
 118:   ;
 119:  IF DO[11:OFF:Called Pallet Stop Ver]=ON,JMP LBL[100] ;
 120:  CALL CLEAR_IO    ;
 121:   ;
 122:  IF DI[25:OFF:Tote In Gripper]=ON,JMP LBL[100] ;
 123:  CALL CLEARDATA    ;
 124:   ;
 125:   ;
 126:  LBL[100] ;
 127:  DO[1:ON :Called Home]=OFF ;
/POS
/END
