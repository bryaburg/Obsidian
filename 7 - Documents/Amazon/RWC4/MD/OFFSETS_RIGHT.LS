/PROG  OFFSETS_RIGHT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3219;
CREATE		= DATE 18-04-23  TIME 06:23:38;
MODIFIED	= DATE 20-03-09  TIME 06:42:12;
FILE_NAME	= SIM_OFFS;
VERSION		= 0;
LINE_COUNT	= 191;
MEMORY_SIZE	= 3731;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !This program is used to ;
   2:  !populate the place position ;
   3:  !register based off of what ;
   4:  !pallet, row and position ;
   5:  !to place the tote ;
   6:   ;
   7:  !Zero out the vision offset and ;
   8:  !place postion register before ;
   9:  !populating the new position ;
  10:   ;
  11:  R[72:Vison Pass/Fail]=0    ;
  12:  R[75:Vision X Found]=0    ;
  13:  R[76:Vision Y Found]=0    ;
  14:  R[77:Vision R Found]=0    ;
  15:   ;
  16:  PR[25:Vision Place Off]=PR[26:Blank Offset]    ;
  17:  PR[11:Pallet Place]=PR[26:Blank Offset]    ;
  18:   ;
  19:  !Set the user frame of the robot ;
  20:  !depending on what pallet you ;
  21:  !are placing on ;
  22:   ;
  23:  UFRAME_NUM=R[1:Pallet Position] ;
  24:   ;
  25:  !Set position register values ;
  26:  !For tote X&Y&R place position ;
  27:   ;
  28:  !Tote #1 ;
  29:   ;
  30:  IF R[4:Tote Position]<>1,JMP LBL[12] ;
  31:   ;
  32:  PR[11,1:Pallet Place]=R[50:Tote 1 X Offset]    ;
  33:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]-180    ;
  34:   ;
  35:  IF R[1:Pallet Position]<4,JMP LBL[30] ;
  36:   ;
  37:  PR[11,2:Pallet Place]=R[51:Tote 1 Y Offset]    ;
  38:   ;
  39:  JMP LBL[31] ;
  40:  LBL[30] ;
  41:   ;
  42:  PR[11,2:Pallet Place]=R[51:Tote 1 Y Offset]*(-1)    ;
  43:   ;
  44:  LBL[31] ;
  45:   ;
  46:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  47:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]-180    ;
  48:   ;
  49:  JMP LBL[17] ;
  50:  LBL[12] ;
  51:   ;
  52:  !Tote #2 ;
  53:   ;
  54:  IF R[4:Tote Position]<>2,JMP LBL[13] ;
  55:   ;
  56:  PR[11,1:Pallet Place]=R[52:Tote 2 X Offset]    ;
  57:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]    ;
  58:   ;
  59:  IF R[1:Pallet Position]<4,JMP LBL[32] ;
  60:   ;
  61:  PR[11,2:Pallet Place]=R[53:Tote 2 Y Offset]    ;
  62:   ;
  63:  JMP LBL[33] ;
  64:  LBL[32] ;
  65:   ;
  66:  PR[11,2:Pallet Place]=R[53:Tote 2 Y Offset]*(-1)    ;
  67:   ;
  68:  LBL[33] ;
  69:   ;
  70:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  71:  PR[11,6:Pallet Place]=R[62:Tote 1&2 R Ang]    ;
  72:   ;
  73:  JMP LBL[17] ;
  74:  LBL[13] ;
  75:   ;
  76:  !Tote #3 ;
  77:   ;
  78:  IF R[4:Tote Position]<>3,JMP LBL[14] ;
  79:   ;
  80:  PR[11,1:Pallet Place]=R[54:Tote 3 X Offset]    ;
  81:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
  82:   ;
  83:  IF R[1:Pallet Position]<4,JMP LBL[34] ;
  84:   ;
  85:  PR[11,2:Pallet Place]=R[55:Tote 3 Y Offset]    ;
  86:   ;
  87:  JMP LBL[35] ;
  88:  LBL[34] ;
  89:   ;
  90:  PR[11,2:Pallet Place]=R[55:Tote 3 Y Offset]*(-1)    ;
  91:   ;
  92:  LBL[35] ;
  93:   ;
  94:  PR[11,5:Pallet Place]=R[61:Tote 1&2 W Ang]    ;
  95:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
  96:   ;
  97:  JMP LBL[17] ;
  98:  LBL[14] ;
  99:   ;
 100:  !Tote #4 ;
 101:   ;
 102:  IF R[4:Tote Position]<>4,JMP LBL[15] ;
 103:   ;
 104:  PR[11,1:Pallet Place]=R[56:Tote 4 X Offset]    ;
 105:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 106:   ;
 107:  IF R[1:Pallet Position]<4,JMP LBL[36] ;
 108:   ;
 109:  PR[11,2:Pallet Place]=R[57:Tote 4 Y Offset]    ;
 110:   ;
 111:  JMP LBL[37] ;
 112:  LBL[36] ;
 113:   ;
 114:  PR[11,2:Pallet Place]=R[57:Tote 4 Y Offset]*(-1)    ;
 115:   ;
 116:  LBL[37] ;
 117:   ;
 118:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 119:   ;
 120:  JMP LBL[17] ;
 121:  LBL[15] ;
 122:   ;
 123:  !Tote #5 ;
 124:   ;
 125:  IF R[4:Tote Position]<>5,JMP LBL[17] ;
 126:   ;
 127:  PR[11,1:Pallet Place]=R[58:Tote 5 X Offset]    ;
 128:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 129:   ;
 130:  IF R[1:Pallet Position]<4,JMP LBL[38] ;
 131:   ;
 132:  PR[11,2:Pallet Place]=R[59:Tote 5 Y Offset]    ;
 133:   ;
 134:  JMP LBL[39] ;
 135:  LBL[38] ;
 136:   ;
 137:  PR[11,2:Pallet Place]=R[59:Tote 5 Y Offset]*(-1)    ;
 138:   ;
 139:  LBL[39] ;
 140:   ;
 141:  PR[11,6:Pallet Place]=R[64:Tote 3&4&5 R Ang]    ;
 142:   ;
 143:  LBL[17] ;
 144:   ;
 145:  !Set the Z height for the ;
 146:  !Row to place the tote ;
 147:   ;
 148:  IF R[3:Pallet Level]<>1,JMP LBL[20] ;
 149:   ;
 150:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]    ;
 151:  PR[14,3:Above Pallet]=1300    ;
 152:   ;
 153:  JMP LBL[25] ;
 154:  LBL[20] ;
 155:   ;
 156:  IF R[3:Pallet Level]<>2,JMP LBL[21] ;
 157:   ;
 158:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]    ;
 159:  PR[14,3:Above Pallet]=1050    ;
 160:   ;
 161:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 162:   ;
 163:  JMP LBL[25] ;
 164:  LBL[21] ;
 165:   ;
 166:  IF R[3:Pallet Level]<>3,JMP LBL[22] ;
 167:   ;
 168:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]    ;
 169:  PR[14,3:Above Pallet]=800    ;
 170:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 171:   ;
 172:  JMP LBL[25] ;
 173:  LBL[22] ;
 174:   ;
 175:  IF R[3:Pallet Level]<>4,JMP LBL[23] ;
 176:   ;
 177:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]    ;
 178:  PR[14,3:Above Pallet]=550    ;
 179:   ;
 180:  PR[30,2:Vision Pic Offt]=R[70:Vison Y Off 3&5]    ;
 181:   ;
 182:  JMP LBL[25] ;
 183:  LBL[23] ;
 184:   ;
 185:  IF R[3:Pallet Level]<>5,JMP LBL[25] ;
 186:   ;
 187:  PR[11,3:Pallet Place]=R[60:Tote Row1 Z Ht]+R[65:Tote Row 2 Z Ht]+R[66:Tote Row 3-4 Zht]+R[66:Tote Row 3-4 Zht]+R[67:Tote Row 5 Z Ht]    ;
 188:  PR[14,3:Above Pallet]=300    ;
 189:  PR[30,2:Vision Pic Offt]=R[71:Vison Y Off 2&4]    ;
 190:   ;
 191:  LBL[25] ;
/POS
/END
